plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'com.github.ben-manes.versions' version '0.28.0'
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'jacoco'

defaultTasks 'run'

mainClassName = 'pl.michalkowol.BootKt'
jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task fatJar(type: Jar) {
    baseName = "${project.name}-assembly"
    manifest = jar.manifest
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

sourceSets {
    integrationTest {
        kotlin {
            srcDirs += 'src/it/kotlin'
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
        resources.srcDirs += 'src/it/resources'
    }
}

task integrationTest(group: 'verification', type: Test, description: 'Runs integration tests.') {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task stage(dependsOn: ['fatJar', 'build', 'clean'])
build.mustRunAfter clean
fatJar.mustRunAfter build
stage.dependsOn(fatJar)

jacoco {
    toolVersion = '0.8.2'
}

jacocoTestReport {
    reports {
        csv.enabled false
        html.enabled true
        xml.enabled true
    }
    dependsOn test
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    ext.rinca = '0.12.0'

    compile 'org.slf4j:slf4j-api:1.7.30'
    compile 'ch.qos.logback:logback-classic:1.2.3'

    compile 'com.sparkjava:spark-core:2.9.1'
    compile 'com.google.inject:guice:4.2.2'
    compile 'com.typesafe:config:1.4.0'
    compile 'com.github.kittinunf.result:result:3.0.0'

    compile "com.github.softwareberg.rinca:httpclient:$rinca"
    compile "com.github.softwareberg.rinca:json:$rinca"

    //compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    //compile("org.jetbrains.kotlin:kotlin-reflect")
    //compile("org.jetbrains.kotlin:kotlin-stdlib-common:1.3.41")

    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0-M2"

    testCompile 'junit:junit:4.13'
    testCompile 'com.natpryce:hamkrest:1.7.0.2'
    testCompile 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'

    integrationTestCompile 'com.despegar:spark-test:1.1.8'
}

wrapper {
    gradleVersion = '6.2.1'
}
